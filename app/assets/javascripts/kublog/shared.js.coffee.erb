# Global helpers should be used in comments as well
window.setErrors = (model, errors) ->
  for attribute, error of errors
    $label = $("label[for=#{model}_#{attribute}]")
    $field = $("##{model}_#{attribute}")
    unless $label.size() is 0
      $label.addClass('error')
      $(errorTemplate(error[0])).insertAfter($label)
    else
      $field.addClass('error')
      $(errorTemplate(error[0])).insertAfter($field)
    
window.resetErrors = ->
  $('.error').removeClass('error')
  $('small.error_description').remove()
    
window.errorTemplate = (error) -> "<small class='error_description'>#{error}</small>"

window.kublogPath = '<%= Kublog.root_path %>'

class KublogForm
  validatesPresenceOf: (input, attr) ->
    return if input.val() isnt ""
    @addError(attr, "can't be blank")

  validatesEmailField: (input, attr) ->
    emailPattern = /^([\w.-]+)@([\w.-]+)\.([a-zA-Z.]{2,6})$/i
    return if input.val().match(emailPattern)
    @addError(attr,"doesn't have a valid format")

  addErrors: (errors)->
    for attr, error of errors
      @addError(attr, error)

  addError: (attr, error)->
    @errors ?= {}
    @errors[attr] = [error]

  validate: ->
    @errors = undefined
    @run_validations()
    @errors is undefined

window.KublogForm =  KublogForm
